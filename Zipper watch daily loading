# zip table dim_user_zip
# First day loading
insert overwrite table dim_user_zip partition (dt = '9999-12-31') 
select data.id, 
       concat(substr(data.name, 1, 1), '*') name, 
       concat(substr(data.phone_num, 1, 3), '*') phone_num, 
       data.user_level, 
       data.birthday, 
       data.gender, 
       data.create_time, 
       data.operate_time, 
       '2022-06-08' start_date, 
       '9999-12-31' end_date
from ods_user_info_inc
where dt = '2022-06-08' and type = 'bootstrap-insert'; 
# Daily loading
set hive.exec.dynamic.partition.mode=nonstrict; 
insert overwrite table dim_user_zip partition (dt) 
select id, 
       name, 
       phone_num, 
       user_level, 
       birthday, 
       gender, 
       create_time, 
       operate_time, 
       start_date, 
       if(rn = 2, date_sub('2022-06-09', 1), end_date) end_date, 
       if(rn = 1, '9999-12-31', date_sub('2022-06-09', 1)) dt 
from ( 
         select id, 
                name, 
                phone_num, 
                user_level, 
                birthday, 
                gender, 
                create_time, 
                operate_time, 
                start_date, 
                end_date, 
                row_number() over(partition by id order by start_date desc) rn
         from (
                  select id, 
                         name, 
                         phone_num, 
                         user_level, 
                         birthday, 
                         gender, 
                         create_time, 
                         operate_time, 
                         start_date, 
                         end_date 
                  from dim_user_zip 
                  where dt = '9999-12-31' 
                  union
                  select id, 
                         concat(substr(name, 1, 1), '*') name,
                         concat(substr(phone_num, 1, 3), '*') phone_num, 
                         user_level, 
                         birthday, 
                         gender, 
                         create_time, 
                         operate_time, 
                         '2022-06-09' start_date, 
                         '9999-12-31' end_date
                  from (
                           select data.id,
                                  data.name,
                                  data.phone_num,
                                  data.email,
                                  data.user_level,
                                  data.birthday,
                                  data.gender,
                                  data.create_time,
                                  data.operate_time,
                                  row_number() over (partition by data.id order by ts desc) rn 
                           from ods_user_info_inc
                           where dt = '2022-06-09'
                       ) t1 
                  where rn = 1
              ) t2 
     ) t3; 
